*** Settings ***

Library  Selenium2Library  timeout=5 seconds  implicit_wait=3 seconds
Resource  keywords.txt
Resource  cover_keywords.txt
Variables  plone/app/testing/interfaces.py

Suite Setup  Start Browser and Log In
Suite Teardown  Close Browser

*** Variables ***

${row_button_selector} =  a#btn-row
${column_button_selector} =  a#btn-column
${tile_button_selector} =  a#btn-tile
${row_drop_area_selector} =  div.row .ui-droppable
${column_drop_area_selector} =  div.row .ui-droppable
${tile_drop_area_selector} =  div.column .ui-droppable
${tile_cancel_area_selector} =  div.modal-backdrop
${basic_tile_name} =  collective.cover.basic

*** Test cases ***

Test Basic Layout Operations
    # XXX: should we create the cover object programmatically?
    Create Cover  Title  Description  Empty layout
    Click Link  link=Layout
    # empty layout has one column and one row by default
    # TODO: test if there is 1 row and 1 column
    #       this can be done using Xpath Should Match X Times
    #       http://rtomac.github.com/robotframework-selenium2library/doc/Selenium2Library.html#Xpath%20Should%20Match%20X%20Times

    # Save button must be disabled as no change has taken place yet
    Page Should Contain  Saved
    Add Row
    Add Column
    # TODO: test if there are 2 rows and 2 columns

    # status changed; now we have a Save button
    Page Should Contain  Save
    Click Element  css=a#btn-save.btn

    # load layout again, it has to be the new one
    Click Link  link=Layout
    # TODO: test if there are 2 rows and 2 columns
    Delete Column
    Delete Row
    # status changed; now we have a Save button
    # TODO: test if there is 1 row and 1 column
    Page Should Contain  Save
    Click Element  css=a#btn-save.btn

    Add Tile
    Cancel Add Tile
    Add Tile
    Select Tile to Add  ${basic_tile_name}
    Delete Tile

*** Keywords ***

Add Row
    Drag And Drop  css=${row_button_selector}  css=${row_drop_area_selector}

Add Column
    Drag And Drop  css=${column_button_selector}  css=${column_drop_area_selector}

Delete Row
    Click Element  css=div.row button.close

Delete Column
    Click Element  css=div.column button.close

Add Tile
    Drag And Drop  css=${tile_button_selector}  css=${column_drop_area_selector}

Cancel Add Tile
    Element Should Be Visible  css=${tile_cancel_area_selector}
    Click Element  css=${tile_cancel_area_selector}
    Element Should Not Be Visible  css=${tile_cancel_area_selector}

Select Tile to Add
    [arguments]  ${tile}

    # XXX: refactor this to allow tile selection
    Click Element  css=div.tile-select-button

Delete Tile
    Click Element  css=div.tile button.close
